* MAINPROG - TEST PROGRAM *

 ORG $4090
 JMP PROG

********************************

HIRESL EQU $FB
HIRESH EQU HIRESL+$1
SHPL EQU $FD
SHPH EQU SHPL+$1
PLL EQU $56
PLH EQU PLL+$1
MAPL EQU $F9
MAPH EQU MAPL+$1
MAPTAB EQU $4000

PX DS 1
PY DS 1
SX DS 1
SY DS 1
HX DS 1
HY DS 1
VX DS 1
VY DS 1
QUAD1 DS 1
QUAD2 DS 1
QUAD3 DS 1
QUAD4 DS 1
QUADO DS 1
QUAD DS 1
QL1 DS 1
QNUM DS 1
SHPN DS 1
ENTRY DS 1
LNGH DS 1
CYCLE DS 1
DAY DS 1
DAYSHP DS 1
FDAY DS 1
WEEK DS 1
SUNVAR DS 1
VAR1 DS 1
VAR2 DS 1
VAR3 DS 1

********************************

PROG JSR STARLOAD
 LDA $C050
 LDA $C052
 LDA $C057
 JSR VARINIT
 JMP MAPDRAW

********************************

STARLOAD LDX #$00
SLOAD1 LDA SDATA1,X
 BEQ SLOAD2
 JSR $FDED
 INX
 JMP SLOAD1

SDATA1 HEX 8D84
 ASC "BLOADBORDER"
 HEX 8D00

SLOAD2 JSR $1B00
 LDX #$00
 BIT $C089
 BIT $C089
SLOAD3 LDA SDATA2,X
 CMP #$00
 BEQ SLOAD4
 JSR $FDED
 INX
 JMP SLOAD3

SDATA2 HEX 8D84
 ASC "BLOADMAPPARMS"
 HEX 8D00

SLOAD4 LDX #$00
SLOAD5 LDA SDATA3,X
 CMP #$00
 BEQ SLOAD6
 JSR $FDED
 INX
 JMP SLOAD5

SDATA3 HEX 8D84
 ASC "BLOADSHAPETAB2"
 HEX 8D00

SLOAD6 LDA #$06
 STA QL1
 STA QUAD1
 LDA #$00
 STA QNUM
 JSR QUADLOAD
 LDA #$07
 STA QL1
 STA QUAD3
 LDA #$02
 STA QNUM
 JSR QUADLOAD
 LDA #$0A
 STA QL1
 STA QUAD2
 LDA #$01
 STA QNUM
 JSR QUADLOAD
 LDA #$0B
 STA QL1
 STA QUAD4
 LDA #$03
 STA QNUM
 JSR QUADLOAD
 RTS

********************************

VARINIT LDA #$00
 STA VAR1
 STA VAR2
 STA VAR3
 STA CYCLE
 STA WEEK
 STA DAY
 STA FDAY
 LDA #$01
 STA QUAD
 LDA #28
 STA PX
 STA PY
 LDA #$03
 STA SUNVAR
 RTS

********************************

QUADLOAD BIT $C081
 BIT $C081
 LDX #$00
QLOAD1 LDA QDATA1,X
 BEQ QLOAD2
 JSR $FDED
 INX
 JMP QLOAD1

QDATA1 HEX 8D84
 ASC "BLOADMAPTAB"
 HEX 00

QLOAD2 LDX QL1
 LDA QLTAB2,X
 JSR $FDED
 LDX #$00
QLOAD3 LDA QDATA2,X
 BEQ QLOAD4
 JSR $FDED
 INX
 JMP QLOAD3

QDATA2 ASC ",A$"
 HEX 00

QLOAD4 LDA #$C4
 JSR $FDED
 LDX QNUM
 LDA QLTAB,X
 JSR $FDED
 LDX #$00
QLOAD5 LDA QDATA3,X
 BEQ QLOAD6
 JSR $FDED
 INX
 JMP QLOAD5

QDATA3 ASC "00"
 HEX 8D00

QLOAD6 RTS

********************************

INPUT LDA $C000
 BPL INPASS
 BIT $C010
 STA ENTRY
 LDA #$00
 STA VAR1
 STA VAR2
 STA VAR3
 LDA ENTRY
 CMP #$CD
 BNE INPUT2
 JMP DOWN
INPUT2 CMP #$CA
 BNE INPUT3
 JMP LEFT
INPUT3 CMP #$CC
 BNE INPUT4
 JMP RIGHT
INPUT4 CMP #$C9
 BNE INPUT5
 JMP UP
INPUT5 JMP INPUT

INPASS INC VAR2
 LDA VAR2
 BNE INPASSE
 INC VAR3
 LDA VAR3
 CMP #$17
 BNE INPASSE
INPASS2 LDA #$00
 STA VAR3
 STA VAR2
 LDA CYCLE
 BNE INPASS1
 INC CYCLE
 JMP MAPDRAW
INPASS1 DEC CYCLE
 JMP MAPDRAW
INPASSE JMP INPUT

********************************

STATUS LDA FDAY
 CMP #$0A
 BLT STATX
 LDA #$00
 STA FDAY
 INC DAY
STATX LDA DAY
 CMP #$20
 BPL STAT2
 LDA CYCLE
 BNE STAT1
 LDX SUNVAR
 LDA STAT1D,X
 JMP STAT7
STAT1 LDX SUNVAR
 LDA STAT2D,X
 JMP STAT7
STAT2 CMP #$24
 BPL STAT4
 LDA CYCLE
 BNE STAT3
 LDX SUNVAR
 LDA STAT3D,X
 JMP STAT7
STAT3 LDX SUNVAR
 LDA STAT4D,X
 JMP STAT7
STAT4 CMP #$34
 BPL STAT5
 LDA #$1E
 JMP STAT7
STAT5 CMP #$38
 BPL STAT6
 LDA #$1F
 JMP STAT7
STAT6 LDA #$00
 STA DAY
 INC WEEK
 LDA WEEK
 CMP #14
 BLT STAT6A
 LDA #$00
 STA WEEK
STAT6A LDX WEEK
 LDA SUNS,X
 STA SUNVAR
STAT6C JMP STATUS

STAT1D HEX 0E12161A
STAT2D HEX 1014181C
STAT3D HEX 0F13171B
STAT4D HEX 1115191D

SUNS HEX 03030300000201
 HEX 01030300000201

STAT7 STA DAYSHP
 STA SHPN
 LDA #$01
 STA HX
 LDA #$14
 STA HY
 JSR BDRAW
 JMP INPUT

********************************

DOWN INC PY
 INC DAY
 LDA PY
 CMP #25
 BLT DOWNE
 LDA QUAD
 CMP #$02
 BEQ DOWN1
 CMP #$04
 BEQ DOWN1
 LDA PY
 CMP #32
 BNE DOWNE
 LDA #$00
 STA PY
 INC QUAD
DOWNE JMP MAPDRAW
DOWN1 DEC QUAD
 LDA #$D4
 STA SHPH
 LDA #$D0
 STA PLH
 JSR MOVESUB
 LDA #$DC
 STA SHPH
 LDA #$D8
 STA PLH
 JSR MOVESUB
 LDA QUAD2
 STA QUAD1
 LDA QUAD4
 STA QUAD3
 LDX QUAD1
 LDA DOWNTAB,X
 STA QUAD2
 STA QL1
 LDA #$01
 STA QNUM
 JSR QUADLOAD
 LDX QUAD3
 LDA DOWNTAB,X
 STA QUAD4
 STA QL1
 LDA #$03
 STA QNUM
 JSR QUADLOAD
 JMP MAPDRAW

********************************

UP DEC PY
 INC DAY
 LDA PY
 CMP #8
 BPL UPE
 LDA QUAD
 CMP #$01
 BEQ UP1
 CMP #$03
 BEQ UP1
 LDA PY
 CMP #$FF
 BNE UPE
 LDA #31
 STA PY
 DEC QUAD
UPE JMP MAPDRAW
UP1 INC QUAD
 LDA #$D0
 STA SHPH
 LDA #$D4
 STA PLH
 JSR MOVESUB
 LDA #$D8
 STA SHPH
 LDA #$DC
 STA PLH
 JSR MOVESUB
 LDA QUAD1
 STA QUAD2
 LDA QUAD3
 STA QUAD4
 LDX QUAD2
 LDA UPTAB,X
 STA QUAD1
 STA QL1
 LDA #$00
 STA QNUM
 JSR QUADLOAD
 LDX QUAD4
 LDA UPTAB,X
 STA QUAD3
 STA QL1
 LDA #$02
 STA QNUM
 JSR QUADLOAD
 JMP MAPDRAW

********************************

LEFT DEC PX
 INC DAY
 LDA PX
 CMP #8
 BPL LEFTE
 LDA QUAD
 CMP #$01
 BEQ LEFT1
 CMP #$02
 BEQ LEFT1
 LDA PX
 CMP #$FF
 BNE LEFTE
 LDA #31
 STA PX
 DEC QUAD
 DEC QUAD
LEFTE JMP MAPDRAW
LEFT1 INC QUAD
 INC QUAD
 LDA #$D0
 STA SHPH
 LDA #$D8
 STA PLH
 JSR MOVESUB
 LDA #$D4
 STA SHPH
 LDA #$DC
 STA PLH
 JSR MOVESUB
 LDA QUAD1
 STA QUAD3
 LDA QUAD2
 STA QUAD4
 LDX QUAD4
 LDA LEFTTAB,X
 STA QUAD2
 STA QL1
 LDA #$01
 STA QNUM
 JSR QUADLOAD
 LDX QUAD3
 LDA LEFTTAB,X
 STA QUAD1
 STA QL1
 LDA #$00
 STA QNUM
 JSR QUADLOAD
 JMP MAPDRAW

********************************

RIGHT INC PX
 INC DAY
 LDA PX
 CMP #25
 BLT RIGHTE
 LDA QUAD
 CMP #$03
 BEQ RIGHT1
 CMP #$04
 BEQ RIGHT1
 LDA PX
 CMP #32
 BNE RIGHTE
 LDA #$00
 STA PX
 INC QUAD
 INC QUAD
RIGHTE JMP MAPDRAW
RIGHT1 DEC QUAD
 DEC QUAD
 LDA #$D0
 STA PLH
 LDA #$D8
 STA SHPH
 JSR MOVESUB
 LDA #$DC
 STA SHPH
 LDA #$D4
 STA PLH
 JSR MOVESUB
 LDA QUAD3
 STA QUAD1
 LDA QUAD4
 STA QUAD2
 LDX QUAD1
 LDA RIGHTTAB,X
 STA QUAD3
 STA QL1
 LDA #$02
 STA QNUM
 JSR QUADLOAD
 LDX QUAD2
 LDA RIGHTTAB,X
 STA QUAD4
 STA QL1
 LDA #$03
 STA QNUM
 JSR QUADLOAD
 JMP MAPDRAW

********************************

MOVESUB BIT $C083
 BIT $C083
 LDA #$00
 STA SHPL
 STA PLL
 LDA PLH
 CLC
 ADC #$04
 STA VAR1
 LDX #$00
MOVESUB1 LDA (SHPL,X)
 STA (PLL,X)
 INC SHPL
 INC PLL
 LDA PLL
 CMP #$00
 BNE MOVESUB1
 INC PLH
 INC SHPH
 LDA PLH
 CMP VAR1
 BLT MOVESUB1
 RTS

********************************

MAPDRAW LDA #$01
 STA SX
 STA SY
 LDA QUAD
 STA QUADO
 LDA #$40
 STA PLH
 LDA #$00
 STA PLL
 LDA PX
 SEC
 SBC #5
 STA VX
 BCS MPDW0
 CLC
 ADC #32
 STA VX
 DEC QUAD
 DEC QUAD
MPDW0 LDA PY
 SEC
 SBC #4
 STA VY
 BCS MPDW1
 CLC
 ADC #32
 STA VY
 DEC QUAD
MPDW1 JSR MAPPLOT
 INC SX
 INC SX
 INC VX
 LDA VX
 CMP #32
 BNE MPDW2
 LDA #$00
 STA VX
 INC QUAD
 INC QUAD
MPDW2 LDA SX
 CMP #$17
 BLT MPDW1
 INC SY
 INC SY
 INC VY
 LDA #$01
 STA SX
 LDA VX
 SEC
 SBC #11
 STA VX
 BCS MPDW3
 CLC
 ADC #32
 STA VX
 DEC QUAD
 DEC QUAD
MPDW3 LDA SY
 CMP #$13
 BPL MPDW4
 LDA VY
 CMP #32
 BNE MPDW1
 LDA #$00
 STA VY
 INC QUAD
 JMP MPDW1
MPDW4 LDA QUADO
 STA QUAD
 JMP NGHTCHK

MAPPLOT LDA SX
 CMP #$0B
 BNE MAPPL0
 LDA SY
 CMP #$09
 BNE MAPPL0
 LDA #$00
 JMP MPPL7
MAPPL0 LDA QUAD
 CMP #$01
 BNE MAPPL1
 JMP MPPL1
MAPPL1 CMP #$02
 BNE MAPPL2
 JMP MPPL2
MAPPL2 CMP #$03
 BNE MAPPL3
 JMP MPPL3
MAPPL3 JMP MPPL4

MPPL1 LDX VY
 LDA MPTAB1L,X
 STA MAPL
 LDA MPTAB1H,X
 STA MAPH
 JMP MPPL5
MPPL2 LDX VY
 LDA MPTAB1L,X
 STA MAPL
 LDA MPTAB2H,X
 STA MAPH
 JMP MPPL5
MPPL3 LDX VY
 LDA MPTAB1L,X
 STA MAPL
 LDA MPTAB3H,X
 STA MAPH
 JMP MPPL5
MPPL4 LDX VY
 LDA MPTAB1L,X
 STA MAPL
 LDA MPTAB4H,X
 STA MAPH
MPPL5 BIT $C080
 LDA MAPL
 CLC
 ADC VX
 STA MAPL
 BCC MPPL6
 INC MAPH
MPPL6 LDX #$00
 LDA (MAPL,X)
 STA (PLL,X)
 STA SHPN
 LDA CYCLE
 BEQ MPPL8
 LDX SHPN
 LDA SHPTABC,X
MPPL7 LDX #$00
 STA (PLL,X)
MPPL8 INC PLL
 RTS

NGHTCHK LDA DAYSHP
 CMP #$1E
 BEQ NCHK1
 CMP #$1F
 BEQ NCHK1
 JMP MAPER
NCHK1 LDA #$00
 STA VAR1
 LDX #$00
NCHK2 LDA NGHTDATA,X
 CMP #$FF
 BEQ NCHK3
 CMP VAR1
 BEQ NCHK4
 INX
 JMP NCHK2
NCHK3 LDY VAR1
 LDA #$0D
 STA MAPTAB,Y
NCHK4 LDX #$00
 INC VAR1
 LDA VAR1
 CMP #$63
 BLT NCHK2
 JMP MAPER

MAPER LDA #$00
 STA VAR1
 STA VAR2
 STA VAR3
 STA VX
MAPER0 LDX VAR3
 LDA MAPERL,X
 STA PLL
 LDA MAPERH,X
 STA PLH
 LDA #$00
 STA VAR2
 STA VX
 INX
 LDA MAPERL,X
 STA VAR1
MAPER1 BIT $C088
 LDX #$00
 LDA (PLL,X)
 TAX
 LDA MAPTAB,X
 CMP #$03
 BEQ MAPER3
MAPER2 INC PLL
 INC VX
 LDA PLL
 CMP #$00
 BNE MAPER20
 INC PLH
MAPER20 LDA PLL
 CMP VAR1
 BLT MAPER1
MAPER2A INC VAR3
 LDA VAR3
 CMP #36
 BLT MAPER0
 JMP MAPER5
MAPER3 INC VAR2
 LDA VAR3
 CMP #$08
 BPL MAPER3A
 LDA VAR2
 CMP #$04
 BLT MAPER2
 LDA VX
 CMP #$04
 BPL MAPER2A
 JMP MAPER4
MAPER3A LDA VAR2
 CMP #$02
 BLT MAPER2
MAPER4 INC PLL
 LDA PLL
 CMP VAR1
 BPL MAPER2A
 LDX #$00
 LDA (PLL,X)
 TAX
 LDA #$0D
 STA MAPTAB,X
 JMP MAPER4
MAPER5 LDX VAR3
 LDA MAPERL,X
 STA PLL
 LDA MAPERH,X
 STA PLH
 INX
 LDA MAPERL,X
 STA VAR1
 LDX #$00
 LDA (PLL,X)
 TAX
 LDA MAPTAB,X
 CMP #$04
 BEQ MAPER8
MAPER7 INC VAR3
 LDA VAR3
 CMP #98
 BLT MAPER5
 JMP MAPDRW
MAPER8 INC PLL
 LDA PLL
 BNE MAPER9
 INC PLH
MAPER9 CMP VAR1
 BPL MAPER7
 LDX #$00
 LDA (PLL,X)
 TAX
 LDA #$0D
 STA MAPTAB,X
 JMP MAPER8

MAPDRW LDA #$01
 STA SX
 STA SY
 LDA #$00
 STA VX
MAPDRW1 LDX VX
 LDA MAPTAB,X
 STA SHPN
 LDA SX
 STA HX
 LDA SY
 STA HY
 JSR BDRAW
 INC VX
 INC SX
 INC SX
 LDA SX
 CMP #$17
 BLT MAPDRW1
 INC SY
 INC SY
 LDA #$01
 STA SX
 LDA SY
 CMP #$13
 BLT MAPDRW1
 JMP STATUS

********************************

BDRAW LDX #$00
 LDA #$02
 STA LNGH
 LDA #$00
 STA VAR3
 BIT $C088
 LDY SHPN
 LDA SHPLO,Y
 STA SHPL
 LDA SHPLH,Y
 STA SHPH
BDRAW1 LDY HY
 LDA YBLOCKL,Y
 STA HIRESL
 LDA YBLOCKH,Y
 STA HIRESH
 LDY HX
BDRAW2 LDA (SHPL,X)
 STA (HIRESL),Y
 LDA HIRESH
 CLC
 ADC #$04
 STA HIRESH
 INC SHPL
 CMP #$40
 BLT BDRAW2
 LDA LNGH
 CMP #$01
 BEQ BDRAW4
 CMP #$00
 BEQ BDRAW5
 INC VAR3
 LDA VAR3
 CMP #$02
 BEQ BDRAW3
 INC HX
 JMP BDRAW1
BDRAW3 INC HY
 DEC LNGH
 DEC HX
 JMP BDRAW1
BDRAW4 INC HX
 DEC LNGH
 JMP BDRAW1
BDRAW5 RTS

********************************

MPTAB1L HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0

MPTAB1H HEX D0D0D0D0D0D0D0D0
 HEX D1D1D1D1D1D1D1D1
 HEX D2D2D2D2D2D2D2D2
 HEX D3D3D3D3D3D3D3D3

MPTAB2H HEX D4D4D4D4D4D4D4D4
 HEX D5D5D5D5D5D5D5D5
 HEX D6D6D6D6D6D6D6D6
 HEX D7D7D7D7D7D7D7D7

MPTAB3H HEX D8D8D8D8D8D8D8D8
 HEX D9D9D9D9D9D9D9D9
 HEX DADADADADADADADA
 HEX DBDBDBDBDBDBDBDB

MPTAB4H HEX DCDCDCDCDCDCDCDC
 HEX DDDDDDDDDDDDDDDD
 HEX DEDEDEDEDEDEDEDE
 HEX DFDFDFDFDFDFDFDF

********************************

MAPERL HEX 000E1C2A384B5E7184
 HEX 898E92969BA0A4A8AD
 HEX B2B6BABEC2C6CACED2
 HEX D7DCE0E4E9EEF2F6FA
 HEX FE171B2B2F484C5C60
 HEX 707A7D838C92959FAF
 HEX B9BCC2CBD1D4DEE3E7
 HEX ECEEF1F3F5F7FAFC01
 HEX 050A0E1315181A1C1E
 HEX 2123282C2E3134373A
 HEX 3D404245484B4E5154

MAPERH HEX D0D0D0D0D0D0D0D0D0
 HEX D0D0D0D0D0D0D0D0D0
 HEX D0D0D0D0D0D0D0D0D0
 HEX D0D0D0D0D0D0D0D0D0
 HEX D0D1D1D1D1D1D1D1D1
 HEX D1D1D1D1D1D1D1D1D1
 HEX D1D1D1D1D1D1D1D1D1
 HEX D1D1D1D1D1D1D1D1D2
 HEX D2D2D2D2D2D2D2D2D2
 HEX D2D2D2D2D2D2D2D2D2
 HEX D2D2D2D2D2D2D2D2D2

********************************

QLTAB HEX B0B4B8C3

QLTAB2 HEX B0B1B2B3B4B5B6B7
 HEX B8B9C1C2C3C4C5C6

********************************

DOWNTAB HEX 0405060708090A0B
 HEX 0C0D0E0F00010203

UPTAB HEX 0C0D0E0F00010203
 HEX 0405060708090A0B

RIGHTTAB HEX 0102030005060704
 HEX 090A0B080D0E0F0C

LEFTTAB HEX 0300010207040506
 HEX 0B08090A0F0C0D0E

********************************

SHPTABC HEX 000B020304050607
 HEX 0C090A0108

********************************

SHPLO HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0
 HEX 0020406080A0C0E0

SHPLH HEX D3D3D3D3D3D3D3D3
 HEX D4D4D4D4D4D4D4D4
 HEX D5D5D5D5D5D5D5D5
 HEX D6D6D6D6D6D6D6D6

********************************

NGHTDATA HEX 1A1B1C2425262728
 HEX 2F303132333A3B3C
 HEX 3D3E464748FF

********************************

YBLOCKH HEX 202021212222232320
 HEX 202121222223232020
 HEX 212122222323

YBLOCKL HEX 008000800080008028
 HEX A828A828A828A850D0
 HEX 50D050D050D0

********************************
