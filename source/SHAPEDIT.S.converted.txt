* SHAPE EDITOR *

 ORG $4000
 JMP PROG

********************************

HIRESL EQU $FB
HIRESH EQU HIRESL+$1
SHPL EQU $FD
SHPH EQU SHPL+$1
PLL EQU $56
PLH EQU PLL+$1
MAPL EQU $F9
MAPH EQU MAPL+$1

LNGH DS 1
TOTAL DS 1
VCOLOR DS 1
SHPN DS 1
ENTRY DS 1
HX DS 1
HY DS 1
PX DS 1
PY DS 1
VARX DS 1
VARY DS 1
COUNTER DS 1
LVAR1 DS 1
LVAR2 DS 1
LVAR3 DS 1
LVAR4 DS 1
CURSOR DS 1
LOCK DS 1
URPS DS 1

********************************

PROG LDA $C050
 LDA $C052
 LDA $C057
 JSR SHAPLOAD
 JSR ERASE
 JSR WORDS
 JSR VARINIT
 JMP INPUT

********************************

ERASE LDA #$60
 STA PLH
 LDA #$00
 STA PLL
 LDY #$00
ERASE1 LDA #$0C
 STA (PLL),Y
 INC PLL
 LDA PLL
 CMP #$00
 BNE ERASE1
 JSR CLRSCR
 JSR BORDER
 RTS

********************************

CLRSCR LDA #$00
 STA HIRESL
 LDA #$20
 STA HIRESH
CLR1 LDY #$00
 LDA #$00
CLR2 STA (HIRESL),Y
 INY
 BNE CLR2
 INC HIRESH
 LDA HIRESH
 CMP #$40
 BLT CLR1
 RTS

********************************

SHAPLOAD LDX #$00
SLOAD1 LDA SDATA,X
 BEQ SLEND
 JSR $FDED
 INX
 JMP SLOAD1
SLEND JMP SLOAD2

SDATA HEX 8D84
 ASC "BLOADSHAPETAB1"
 HEX 8D00

SLOAD2 LDX #$00
SLOAD3 LDA S1DATA,X
 BEQ SLOAD4
 JSR $FDED
 INX
 JMP SLOAD3
SLOAD4 RTS

S1DATA HEX 8D84
 ASC "BLOADHGRASCII"
 HEX 8D00

********************************

BORDER LDA #>BORDATA
 STA PLH
 LDA #<BORDATA
 STA PLL
BORDER1 LDX #$00
 LDA (PLL,X)
 CMP #$FF
 BEQ BORDONE
 STA HX
 INC PLL
 LDA PLL
 CMP #$00
 BNE BORDER2
 INC PLH
BORDER2 LDA (PLL,X)
 STA HY
 INC PLL
 LDA PLL
 CMP #$00
 BNE BORDER3
 INC PLH
BORDER3 LDA (PLL,X)
 STA SHPN
 INC PLL
 LDA PLL
 CMP #$00
 BNE BORDER4
 INC PLH
BORDER4 JSR SDRAW
 JMP BORDER1
BORDONE RTS

********************************

WORDS LDX #$00
 LDA #$00
 STA HX
 LDA #20
 STA HY
WORDS1 LDA WORDTA,X
 BEQ WORDS3
 CMP #$DF
 BEQ WORDS2
 SBC #$9F
 STA SHPN
 STX URPS
 JSR NDRAW
 INC HX
 LDX URPS
 INX
 JMP WORDS1
WORDS2 INC HY
 LDA #$00
 STA HX
 INX
 JMP WORDS1
WORDS3 RTS

WORDTA ASC "I,J,L,M-MOVEMENT."
 ASC "@(SHFT-8)-DUMP"
 ASC "@TABLE._"
 ASC "(CTRL-E)-ERASE."
 ASC "@(SPC,P)-PLOT CMMNDS._"
 ASC "C-COLORS>"
 ASC "@(O)RNG,(W)HT,(B)LCK,(G)RN,_"
 ASC "(CTRL-G),(CTRL-O)-ODD"
 ASC "@COL@CLRS."
 HEX 00

********************************

VARINIT LDA #$02
 STA PX
 STA PY
 LDA #$0C
 STA VCOLOR
 LDA #$00
 STA LOCK
 RTS

********************************

PLOCK LDA LOCK
 CMP #$00
 BNE PLOCK1
 LDA #$01
 STA LOCK
 JMP PLOCK2
PLOCK1 LDA #$00
 STA LOCK
PLOCK2 RTS

********************************

INPUT LDA $C000
 BPL INPPASS
 BIT $C010
 STA ENTRY
 LDA #$00
 STA LVAR1
 STA LVAR2
 STA LVAR3
 LDA ENTRY
 CMP #$CD
 BNE INPUT2
 JMP DOWN
INPUT2 CMP #$CA
 BNE INPUT3
 JMP LEFT
INPUT3 CMP #$CC
 BNE INPUT4
 JMP RIGHT
INPUT4 CMP #$C9
 BNE INPUT5
 JMP UP
INPUT5 CMP #$A0
 BNE INPUT6
 JMP PLOT
INPUT6 CMP #$C3
 BNE INPUT7
 JMP COLOR
INPUT7 CMP #$AA
 BNE INPUT8
 JMP DUMP
INPUT8 CMP #$85
 BNE INPUT9
 JSR ERASE
 JMP INPUT
INPUT9 CMP #$D0
 BNE INPUT10
 JSR PLOCK
INPUT10 JMP INPUT

INPPASS INC LVAR1
 LDA LVAR1
 CMP #$00
 BNE IPASSE
 INC LVAR2
 LDA LVAR2
 CMP #$00
 BNE IPASSE
 LDA CURSOR
 CMP #$0F
 BNE INPPASS2
 LDA VCOLOR
INPPASS1 STA CURSOR
 STA SHPN
 LDA #$13
 STA HX
 LDA #$0E
 STA HY
 JSR SDRAW
 JMP IPASSE
INPPASS2 LDA #$0F
 JMP INPPASS1
IPASSE JMP INPUT

********************************

DOWN INC PY
 LDA PY
 CMP #$11
 BLT DOWN2
 LDA #$10
 STA PY
 JMP INPUT
DOWN2 LDX PY
 DEX
 LDA MATTABL,X
 CLC
 ADC PX
 STA PLL
 LDA #$60
 STA PLH
 LDX #$00
 LDA (PLL,X)
 STA SHPN
 LDX PY
 DEX
 TXA
 STA HY
 LDA PX
 STA HX
 JSR SDRAW
 LDA #$11
 STA SHPN
 LDA PY
 STA HY
 JSR SDRAW
 LDA LOCK
 CMP #$00
 BEQ DOWN7
 JMP PLOT
DOWN7 JMP INPUT

********************************

UP DEC PY
 LDA PY
 CMP #$01
 BPL UP2
 LDA #$01
 STA PY
 JMP INPUT
UP2 LDX PY
 INX
 LDA MATTABL,X
 CLC
 ADC PX
 STA PLL
 LDA #$60
 STA PLH
 LDX #$00
 LDA (PLL,X)
 STA SHPN
 LDX PY
 INX
 TXA
 STA HY
 LDA PX
 STA HX
 JSR SDRAW
 LDA #$11
 STA SHPN
 LDA PY
 STA HY
 JSR SDRAW
 LDA LOCK
 CMP #$00
 BEQ UP7
 JMP PLOT
UP7 JMP INPUT

********************************

LEFT DEC PX
 LDA PX
 CMP #$01
 BPL LEFT2
 LDA #$01
 STA PX
 JMP INPUT
LEFT2 LDX PY
 LDA MATTABL,X
 CLC
 ADC PX
 CLC
 ADC #$01
 STA PLL
 LDA #$60
 STA PLH
 LDX #$00
 LDA (PLL,X)
 STA SHPN
 LDA PY
 STA HY
 LDX PX
 INX
 TXA
 STA HX
 JSR SDRAW
 LDA #$11
 STA SHPN
 LDA PX
 STA HX
 JSR SDRAW
 LDA LOCK
 CMP #$00
 BEQ LEFT7
 JMP PLOT
LEFT7 JMP INPUT

********************************

RIGHT INC PX
 LDA PX
 CMP #$0F
 BLT RIGHT2
 LDA #$0E
 STA PX
 JMP INPUT
RIGHT2 LDX PY
 LDA MATTABL,X
 CLC
 ADC PX
 SEC
 SBC #$01
 STA PLL
 LDA #$60
 STA PLH
 LDX #$00
 LDA (PLL,X)
 STA SHPN
 LDA PY
 STA HY
 LDX PX
 DEX
 TXA
 STA HX
 JSR SDRAW
 LDA #$11
 STA SHPN
 LDA PX
 STA HX
 JSR SDRAW
 LDA LOCK
 CMP #$00
 BEQ RIGHT7
 JMP PLOT
RIGHT7 JMP INPUT

********************************

PLOT LDA #$60
 STA MAPH
 LDX PY
 LDA MATTABL,X
 CLC
 ADC PX
 STA MAPL
 LDA VCOLOR
 LDY #$00
 STA (MAPL),Y
 LDA #$00
 STA TOTAL
 STA COUNTER
 LDY #$12
 LDA SHPLO,Y
 STA SHPL
 LDA SHPLH,Y
 STA SHPH
 LDA #$01
 STA MAPL
 LDA #$02
 STA VARY
PLOT1 LDA COUNTER
 CMP #$07
 BLT PLOT2
 BNE PLOT1A
 LDA #$01
 STA VARY
PLOT1A LDA COUNTER
 CMP #$0F
 BLT PLOT2
 BNE PLOT1B
 LDA #$09
 STA VARY
PLOT1B LDA COUNTER
 CMP #$17
 BLT PLOT2
 BNE PLOT1C
 LDA #$09
 STA VARY
PLOT1C LDA COUNTER
 CMP #$20
 BLT PLOT2
PLOT1D JSR BDRAW
 JMP INPUT
PLOT2 LDA #$00
 STA LVAR3
 STA VARX
 STA TOTAL
PLOT2A LDX #$00
 LDA (MAPL,X)
 CMP #$0B
 BNE PLOT2B
 LDA #$01
 STA LVAR3
 JMP PLOT2C3
PLOT2B CMP #$0E
 BNE PLOT2C
 LDA #$01
 STA LVAR3
 JMP PLOT2C3
PLOT2C CMP #$0C
 BNE PLOT2C3
PLOT2CB INC VARX
 INC MAPL
 LDA VARX
 CMP #$07
 BLT PLOT2A
 LDA LVAR3
 CMP #$00
 BEQ PLOT2CX
 LDA TOTAL
 CLC
 ADC #$80
 STA TOTAL
PLOT2CX LDA COUNTER
 CMP #$07
 BCS PLOT2C1
 LDX VARY
 LDA MATTABL,X
 STA MAPL
 INC VARY
 INC MAPL
 JSR PLOTD
 JMP PLOT1
PLOT2C1 CMP #$0F
 BLT PLOT2C2
 CMP #$17
 BCS PLOT2C2
 LDX VARY
 LDA MATTABL,X
 STA MAPL
 INC MAPL
 INC VARY
 JSR PLOTD
 JMP PLOT1
PLOT2C2 LDX VARY
 LDA MATTABH,X
 STA MAPL
 INC VARY
 JSR PLOTD
 JMP PLOT1
PLOT2C3 LDX VARX
 LDA SHAPEV1,X
 CLC
 ADC TOTAL
 STA TOTAL
 JMP PLOT2CB
PLOTD LDY COUNTER
 LDA TOTAL
 STA (SHPL),Y
 INC COUNTER
 RTS

********************************

COLOR LDA $C000
 BPL COLOR
 BIT $C010
 CMP #$CF
 BNE COLOR1
 LDA #$0B
 STA VCOLOR
 JMP INPUT
COLOR1 CMP #$D7
 BNE COLOR2
 LDA #$0D
 STA VCOLOR
 JMP INPUT
COLOR2 CMP #$C2
 BNE COLOR3
 LDA #$0C
 STA VCOLOR
 JMP INPUT
COLOR3 CMP #$C7
 BNE COLOR4
 LDA #$0F
 STA VCOLOR
COLOR4 CMP #$87
 BNE COLOR5
 LDA #$10
 STA VCOLOR
COLOR5 CMP #$8F
 BNE COLOR6
 LDA #$0E
 STA VCOLOR
COLOR6 JMP INPUT

********************************

DUMP LDA $C051
 LDA $C054
 LDY #$12
 LDA SHPLO,Y
 STA SHPL
 LDA SHPLH,Y
 STA SHPH
 LDA #$8D
 JSR $FDED
DUMP1 LDX #$00
 LDA (SHPL,X)
 JSR $FDDA
DUMP2 LDA $C000
 BPL DUMP2
 BIT $C010
 INC SHPL
 LDA SHPL
 CMP #$A8
 BLT DUMP1
 LDA $C050
 LDA $C052
 LDA $C057
 JMP INPUT

********************************

SDRAW LDY HY
 LDA YBLOCKL,Y
 STA HIRESL
 LDA YBLOCKH,Y
 STA HIRESH
 LDY SHPN
 LDA SHPLO,Y
 STA SHPL
 LDA SHPLH,Y
 STA SHPH
 LDY HX
 LDX #$00
SDRAW1 LDA (SHPL,X)
 STA (HIRESL),Y
 LDA HIRESH
 CLC
 ADC #$04
 STA HIRESH
 INC SHPL
 CMP #$40
 BLT SDRAW1
 RTS

********************************

NDRAW LDY HY
 LDA YBLOCKL,Y
 STA HIRESL
 LDA YBLOCKH,Y
 STA HIRESH
 LDY SHPN
 LDA ASCLO,Y
 STA SHPL
 LDA ASCHI,Y
 STA SHPH
 LDY HX
 LDX #$00
NDRAW1 LDA (SHPL,X)
 STA (HIRESL),Y
 LDA HIRESH
 CLC
 ADC #$04
 STA HIRESH
 INC SHPL
 CMP #$40
 BLT NDRAW1
 RTS

********************************

BDRAW LDX #$00
 LDA #$00
 STA LVAR4
BDRAW2A LDX LVAR4
 LDA PTAB1,X
 CMP #$FF
 BNE BDRAW1A
 RTS
BDRAW1A STA HX
 INX
 INC LVAR4
 LDA PTAB1,X
 STA HY
 INC LVAR4
 LDX #$00
 LDA #$02
 STA LNGH
 LDA #$00
 STA LVAR3
 LDY #$12
 LDA SHPLO,Y
 STA SHPL
 LDA SHPLH,Y
 STA SHPH
BDRAW1 LDY HY
 LDA YBLOCKL,Y
 STA HIRESL
 LDA YBLOCKH,Y
 STA HIRESH
 LDY HX
BDRAW2 LDA (SHPL,X)
 STA (HIRESL),Y
 LDA HIRESH
 CLC
 ADC #$04
 STA HIRESH
 INC SHPL
 CMP #$40
 BLT BDRAW2
 LDA LNGH
 CMP #$01
 BEQ BDRAW4
 CMP #$00
 BEQ BDRAW5
 INC LVAR3
 LDA LVAR3
 CMP #$02
 BEQ BDRAW3
 INC HX
 JMP BDRAW1
BDRAW3 INC HY
 DEC LNGH
 DEC HX
 JMP BDRAW1
BDRAW4 INC HX
 DEC LNGH
 JMP BDRAW1
BDRAW5 JMP BDRAW2A

********************************

BORDATA HEX 000007010006020006
 HEX 030006040006050006
 HEX 060006070006080006
 HEX 0900060A00060B0006
 HEX 0C00060D00060E0006
 HEX 0F00080F01050F0205
 HEX 0F03050F04050F0505
 HEX 0F06050F07050F0805
 HEX 0F09050F0A050F0B05
 HEX 0F0C050F0D050F0E05
 HEX 0F0F050F10050F110A
 HEX 0E11060D11060C1106
 HEX 0B11060A1106091106
 HEX 081106071106061106
 HEX 051106041106031106
 HEX 021106011106001109
 HEX 001005000B05000A05
 HEX 000F05000D05000C05
 HEX 000E05
 HEX 000905000805000705
 HEX 000605000505000405
 HEX 000305000205000105
 HEX 120907130906140906
 HEX 150908150A05150B05
 HEX 150C0A140C06130C06
 HEX 120C09120B05120A05
 HEX FF

********************************

MATTABL HEX 00001E2D3C4B5A69
 HEX 788796A5B4C3D2E1F0

MATTABH HEX 0808263544536271
 HEX 808F9EADBCCBDAE9F8


********************************

SHAPEV1 HEX 01020408102040

********************************

PTAB1 HEX 130A1B0A1D0A1F0A
 HEX 1B0C1D0C1F0C
 HEX 1B0E1D0E1F0EFFFF

********************************

SHPLO HEX 0000081018202830
 HEX 38404850586068
 HEX 70788088

SHPLH HEX 5050505050505050
 HEX 50505050505050
 HEX 50505050

********************************

YBLOCKH HEX 202021212222232320
 HEX 202121222223232020
 HEX 212122222323

YBLOCKL HEX 008000800080008028
 HEX A828A828A828A850D0
 HEX 50D050D050D0

********************************

ASCLO HEX 0008101820283038
 HEX 4048505860687078
 HEX 80889098A0A8B0B8
 HEX C0C8D0D8E0E8F0F8
 HEX 0008101820283038
 HEX 4048505860687078
 HEX 80889098A0A8B0B8
 HEX C0C8D0D8E0E8F0F8

ASCHI HEX 9090909090909090
 HEX 9090909090909090
 HEX 9090909090909090
 HEX 9090909090909090
 HEX 9191919191919191
 HEX 9191919191919191
 HEX 9191919191919191
 HEX 9191919191919191
